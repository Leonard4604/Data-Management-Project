:auto LOAD CSV WITH HEADERS FROM 'file:///circuits.csv' AS row
CALL {
  WITH row
  CREATE (:Circuit {id: row.circuitId, name: row.name, location: row.location, country: row.country})
} IN TRANSACTIONS OF 1000 ROWS; 

:auto LOAD CSV WITH HEADERS FROM 'file:///results.csv' AS row
CALL {
  WITH row
  CREATE (:Result {id: row.resultId, race_id: row.raceId, driver_id: row.driverId, constructor_id: row.constuctorId, number: row.number, position: row.position, points: row.points, laps: row.laps, time: row.time, fastestLapTime: row.fastestLapTime, status_id: row.statusId, grid: row.grid})
} IN TRANSACTIONS OF 1000 ROWS; 

:auto LOAD CSV WITH HEADERS FROM 'file:///races.csv' AS row
CALL {
  WITH row
  CREATE (:Race {id: row.raceId, name: row.name, round: row.round, circuit_id: row.circuitId, date: row.date, time: row.time, year: row.year})
} IN TRANSACTIONS OF 1000 ROWS; 

:auto LOAD CSV WITH HEADERS FROM 'file:///constructors.csv' AS row
CALL {
  WITH row
  MERGE (c:Constructor {id: row.constructorId, name: row.name, nationality: row.nationality})
} IN TRANSACTIONS OF 1000 ROWS;

:auto LOAD CSV WITH HEADERS FROM 'file:///drivers.csv' AS row
CALL {
  WITH row
  CREATE (:Driver {id: row.driverId, name: row.forename, surname: row.surname, dob: row.dob, nationality: row.nationality})
} IN TRANSACTIONS OF 1000 ROWS;

:auto LOAD CSV WITH HEADERS FROM 'file:///sprint_results.csv' AS row
CALL {
  WITH row
  MATCH (d:Driver {id: row.driverId})
  MATCH (r:SprintResult {id: row.resultId})
  MERGE (d)-[:PARTICIPATED_IN]->(r)
} IN TRANSACTIONS OF 1000 ROWS;


:auto LOAD CSV WITH HEADERS FROM 'file:///sprint_results.csv' AS row
CALL {
  WITH row
  CREATE (:SprintResult {id: row.resultId, race_id: row.raceId, driver_id: row.driverId, constructor_id: row.constuctorId, number: row.number, position: row.position, points: row.points, laps: row.laps, time: row.time, fastestLapTime: row.fastestLapTime, status_id: row.statusId})
} IN TRANSACTIONS OF 1000 ROWS; 

:auto LOAD CSV WITH HEADERS FROM 'file:///sprint_results.csv' AS row
CALL {
  WITH row
  MATCH (c:Constructor {id: row.constructorId})
  MATCH (r:SprintResult {id: row.resultId})
  MERGE (r)-[:PART_OF]->(c)
} IN TRANSACTIONS OF 1000 ROWS;


:auto LOAD CSV WITH HEADERS FROM 'file:///results.csv' AS row
CALL {
  WITH row
  MATCH (c:Constructor {id: row.constructorId})
  MATCH (r:Result {id: row.resultId})
  MERGE (r)-[:PART_OF]->(c)
} IN TRANSACTIONS OF 1000 ROWS;

/*NEO4J CREATE THE RELATION DROVE_FOR*/
:auto LOAD CSV WITH HEADERS FROM 'file:///results.csv' AS row
CALL {
  WITH row
  MATCH (d:Driver {id: row.driverId})
  MATCH (c:Constructor {id: row.constructorId})
  MERGE (d)-[:DROVE_FOR]->(c)
} IN TRANSACTIONS OF 1000 ROWS

:auto LOAD CSV WITH HEADERS FROM 'file:///results.csv' AS row
CALL {
  WITH row
  MATCH (d:Driver {id: row.driverId})
  MATCH (r:Result {id: row.resultId})
  MERGE (d)-[:PARTICIPATED_IN]->(r)
} IN TRANSACTIONS OF 1000 ROWS;

MATCH (r:Result), (ra:Race) 
WHERE r.race_id = ra.id
MERGE (r)-[:PART_OF]->(ra)

MATCH (r:SprintResult), (ra:Race) 
WHERE r.race_id = ra.id
MERGE (r)-[:PART_OF]->(ra)

:auto LOAD CSV WITH HEADERS FROM 'file:///races.csv' AS row
CALL {
  WITH row
  MATCH (c:Circuit {id: row.circuitId})
  MATCH (r:Race {id: row.raceId})
  MERGE (r)-[:HELD_IN]->(c)
} IN TRANSACTIONS OF 1000 ROWS;

CALL apoc.periodic.iterate(
  "MATCH (c:Constructor) RETURN c",
  "MATCH (d1:Driver)-[:DROVE_FOR]->(c)
   MATCH (d2:Driver)-[:DROVE_FOR]->(c)
   WHERE d1.id < d2.id
   AND NOT EXISTS((d1)-[:SHARED_CONSTRUCTOR]->(d2))
   MERGE (d1)-[r:SHARED_CONSTRUCTOR]->(d2)
   SET r.constructor = c.name",
  {batchSize:10, iterateList:true, parallel:false}
) 

/*1) DRIVERS FOR THE SAME CONSTRUCTOR*/
MATCH (d1:Driver)-[:DROVE_FOR]->(c:Constructor)<-[:DROVE_FOR]-(d2:Driver)
WHERE d1 <> d2
RETURN d1.name + ' ' + d1.surname AS Driver1, 
       d2.name + ' ' + d2.surname AS Driver2, 
       c.name AS Constructor
ORDER BY Constructor, Driver1, Driver2

/*2) WIN RATIO FOR EACH DRIVER*/
MATCH (d:Driver)-[re:PARTICIPATED_IN]->(r:Result)
WITH d, 
     COUNT(re) AS total_races, 
     COUNT(CASE WHEN toInteger(r.position) = 1 THEN 1 END) AS wins
WHERE total_races > 0
RETURN d.name AS forename, 
       d.surname AS surname, 
       wins, 
       total_races, 
       (wins * 100.0) / total_races AS win_ratio
ORDER BY win_ratio DESC;

/*3) DRIVER TREND OVER TIME*/
MATCH (d:Driver {name: "Max", surname: "Verstappen"})-[:PARTICIPATED_IN]->(r:Result)-[:PART_OF]->(ra:Race)
WITH DISTINCT d, r, ra
WITH d.name AS Name, d.surname AS Surname, ra.year AS year, SUM(toInteger(r.points)) AS total_points
RETURN Name, Surname, year, total_points
ORDER BY Name ASC, year ASC;

/*4) Average Finishing Position for Drivers*/
MATCH (d:Driver)-[:PARTICIPATED_IN]->(r:Result)
WITH d.name AS forename, d.surname AS surname, 
     COALESCE(AVG(toInteger(r.position)), 0) AS avg_position
RETURN forename, surname, avg_position
ORDER BY avg_position DESC;

/*5) Best position for each driver per race*/
MATCH (driver:Driver)-[:PARTICIPATED_IN]->(r:Result)-[:PART_OF]->(ra:Race)
WITH driver, ra.name AS race_name, MIN(toInteger(r.position)) AS best_position
WITH driver, race_name, best_position
ORDER BY race_name ASC
RETURN driver.name, driver.surname, race_name, best_position
ORDER BY driver.surname, driver.name, race_name;

/*6) This query calculates the total points for each driver for a particular season.*/
MATCH (d:Driver)-[:PARTICIPATED_IN]->(r:Result)-[:PART_OF]->(ra:Race)
WHERE ra.year = 2023
OPTIONAL MATCH (d)-[:PARTICIPATED_IN]->(sr:SprintResult)-[:PART_OF]->(ra)
WITH d.name AS forename, d.surname AS surname, r.points AS race_points, COALESCE(sr.points, 0) AS sprint_points
WITH forename, surname, SUM(toInteger(race_points) + toInteger(sprint_points)) AS total_points
RETURN forename, surname, total_points
ORDER BY total_points DESC;

/*7) Similar to driver standings, this query sums up points for each constructor for a season.*/
MATCH (c:Constructor)<-[:PART_OF]-(r:Result)-[:PART_OF]->(ra:Race)
WHERE ra.year = 2023
OPTIONAL MATCH (sr:SprintResult)-[:PART_OF]->(ra) 
WHERE r.driver_id = sr.driver_id
WITH c.name AS constructor_name, SUM(toInteger(r.points)) AS race_points, COALESCE(SUM(toInteger(sr.points)), 0) AS sprint_points
WITH constructor_name, (race_points + sprint_points) AS total_points
RETURN constructor_name, total_points
ORDER BY total_points DESC;

/*8) This query identifies the driver with the fastest lap in each race.*/
MATCH (race:Race)<-[:PART_OF]-(result:Result)
WHERE result.fastestLapTime IS NOT NULL AND result.fastestLapTime <> '\N'
WITH race, result, 
     result.fastestLapTime AS lap_time,
     toInteger(split(result.fastestLapTime, ":")[0]) * 60 + 
     toFloat(split(result.fastestLapTime, ":")[1]) AS lap_time_in_seconds
WITH race, MIN(lap_time_in_seconds) AS minLapTime
MATCH (driver:Driver)-[:PARTICIPATED_IN]->(result:Result)-[:PART_OF]->(race)
WHERE toInteger(split(result.fastestLapTime, ":")[0]) * 60 + 
      toFloat(split(result.fastestLapTime, ":")[1]) = minLapTime
RETURN race.name AS race_name, driver.name AS forename, driver.surname AS surname, result.fastestLapTime AS fastest_lap_time, race.year AS year
ORDER BY race.name ASC, race.year ASC;

/*9) Constructor Performance by Circuit*/
MATCH (result:Result)-[:PART_OF]->(race:Race)-[:HELD_IN]->(circuit:Circuit),
      (result)-[:PART_OF]->(constructor:Constructor)
WITH circuit.name AS circuit_name, constructor.name AS constructor_name, 
     AVG(toFloat(result.position)) AS avg_position
WHERE avg_position IS NOT NULL
RETURN circuit_name, constructor_name, avg_position
ORDER BY circuit_name ASC, avg_position ASC;

/*10) Finding Mutual Wins Among Drivers*/
MATCH (d1:Driver)-[:PARTICIPATED_IN]->(r1:Result)-[:PART_OF]->(ra:Race)
MATCH (d2:Driver)-[:PARTICIPATED_IN]->(r2:Result)-[:PART_OF]->(ra)
WHERE toInteger(r1.position) = 1 AND toInteger(r2.position) = 2
AND d1 <> d2
WITH d1, d2, COUNT(ra) AS mutual_wins
RETURN d1.name AS driver_1_name, 
       d1.surname AS driver_1_surname, 
       d2.name AS driver_2_name, 
       d2.surname AS driver_2_surname, 
       mutual_wins
ORDER BY mutual_wins DESC;

/*11) Number of times a driver start as last and finish on podium*/
MATCH (d:Driver)-[:PARTICIPATED_IN]->(r:Result)-[:PART_OF]->(ra:Race)
WITH ra, MAX(toInteger(r.grid)) AS last_position
MATCH (d:Driver)-[:PARTICIPATED_IN]->(r:Result)-[:PART_OF]->(ra:Race)
WHERE toInteger(r.grid) = last_position
AND toInteger(r.position) IN [1, 2, 3]
WITH d, ra, r.position AS position, COUNT(ra) AS wins_from_last_position
RETURN d.name AS forename, 
       d.surname AS surname,
       ra.name, 
       position, 
       wins_from_last_position
ORDER BY wins_from_last_position ASC;